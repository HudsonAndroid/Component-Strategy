plugins {
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

if(flags.isRelease){
    apply plugin: 'com.android.library'
}else{
    apply plugin: 'com.android.application'
}

android {
    compileSdk versions.android.compileSdk

    defaultConfig {
        if(!flags.isRelease){
            applicationId "com.hudson.logic"
        }
        minSdk versions.android.minSdk
        targetSdk versions.android.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 给内部的注解处理器传递参数
        javaCompileOptions {
            annotationProcessorOptions{
                arguments = [
                    HRouterGroup: project.name,// 路由分组直接指定为模块名。 注意不要直接使用name，会被覆盖为main，要引用project.name
                    RouterPkg: hroute_info.packageName // 路由代码生成的包名
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main{
            if(flags.isRelease){
                manifest.srcFile 'src/main/AndroidManifest.xml'
                // 打包时要排除掉dev目录
                java {
                    exclude 'src/main/dev/'
                }
            }else{
                manifest.srcFile 'src/main/dev/AndroidManifest.xml'

                // 增加源码搜索路径 src/main/dev目录
                java{
                    srcDir 'src/main/dev'
                }

                // 参考 https://developer.android.com/studio/build/build-variants#sourcesets
                res.srcDirs = ['src/main/res/', 'src/main/dev/res/']
            }
        }
    }
}

dependencies {
    // 公共依赖，通过common直接将依赖传递过来，因此不再需要手动依赖
//    implementation 'androidx.core:core-ktx:1.7.0'
//    implementation 'androidx.appcompat:appcompat:1.3.0'
//    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // 依赖路由组件
    kapt project(":HRouter-Annotation-Processor")
    implementation project(":HRouter-Api")

    // 依赖基础功能组件
    implementation project(":common")
}